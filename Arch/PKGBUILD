# Maintainer: Evangelos Rigas <erigas@rnd2.org>

pkgname=fanctl-git
pkgver=0.1.0.r0.gfb589a0
pkgrel=1
pkgdesc="A utility to auto-update HWMON paths at boot for fancontrol"
arch=(any)
url="https://git.rnd2.org/erigas/fanctl"
license=('GPL')
depends=('python' 'python-yaml' 'lm_sensors')
makedepends=('git' 'meson' 'pkg-config')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname%-git}::git+https://git.rnd2.org/erigas/fanctl.git")
noextract=()
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'

}


build() {
  meson --prefix /usr -Dsystemddir=/usr/lib/systemd --buildtype=plain "$srcdir/${pkgname%-git}" build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}


